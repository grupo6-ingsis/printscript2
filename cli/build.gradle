plugins {
    id 'org.jetbrains.kotlin.jvm'
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1"
    id 'buildlogic.kotlin-publish-conventions'
    id 'application'
}

repositories {
    mavenCentral()
}
application {
    mainClass = 'org.gudelker.cli.CliKt'
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation project(':utilities')
    implementation("com.github.ajalt.clikt:clikt:5.0.1")
    implementation("com.github.ajalt.clikt:clikt-markdown:5.0.1")
    implementation project(':ast')
    implementation project(':token')
    implementation project(':lexer')
    implementation project(':parser')
    implementation project(':interpreter')
    implementation project(':formatter')
    implementation project(':analyzer')
}

test {
    useJUnitPlatform()
}
tasks.named('check') {
    dependsOn 'ktlintCheck'
}
tasks.register('bumpVersion') {
    doLast {
        def file = file('build.gradle')
        def text = file.text
        def matcher = (text =~ /versionCode = '(\d+)\.(\d+)\.(\d+)'/)
        if (matcher.find()) {
            def major = matcher.group(1).toInteger()
            def minor = matcher.group(2).toInteger()
            def patch = matcher.group(3).toInteger() + 1
            def newVersion = "${major}.${minor}.${patch}"
            def newText = text.replaceFirst(/versionCode = '.*'/, "versionCode = '${newVersion}'")
            file.text = newText
            println "Versión actualizada a ${newVersion}"
        } else {
            throw new GradleException("No se encontró versionCode en build.gradle")
        }
    }
}

tasks.named('publish').configure {
    dependsOn tasks.named('bumpVersion')
}